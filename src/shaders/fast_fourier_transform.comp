#version 450

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(set = 0, binding = 0, rgba32f) uniform readonly image2D PrecomputedData;
layout(set = 0, binding = 1, rgba32f) uniform image2D Buffer0;
layout(set = 0, binding = 2, rgba32f) uniform image2D Buffer1;

layout(push_constant) uniform PushConstants {
    uint size;
    uint stage;
    uint mode;
    uint ping_pong;
} params;

const float PI = 3.1415926;

vec2 ComplexMult(vec2 a, vec2 b) {
	return vec2(a.r * b.r - a.g * b.g, a.r * b.g + a.g * b.r);
}

void HorizontalStepFFT() {
    uvec3 id = gl_GlobalInvocationID;
    if (id.x >= params.size || id.y >= params.size)
        return;
    
    vec4 data = imageLoad(PrecomputedData, ivec2(params.stage, id.x));
    uvec2 inputsIndices = uvec2(data.ba);
    
    if (params.ping_pong == 1u) {
        vec4 buffer_x = imageLoad(Buffer0, ivec2(inputsIndices.x, id.y));
        vec4 buffer_y = imageLoad(Buffer0, ivec2(inputsIndices.y, id.y));
        vec2 result = buffer_x.rg + ComplexMult(data.rg, buffer_y.rg);
        imageStore(Buffer1, ivec2(id.xy), vec4(result, 0.0, 0.0));
    } else {
        vec4 buffer_x = imageLoad(Buffer1, ivec2(inputsIndices.x, id.y));
        vec4 buffer_y = imageLoad(Buffer1, ivec2(inputsIndices.y, id.y));
        vec2 result = buffer_x.rg + ComplexMult(data.rg, buffer_y.rg);
        imageStore(Buffer0, ivec2(id.xy), vec4(result, 0.0, 0.0));
    }
}

void VerticalStepFFT() {
    uvec3 id = gl_GlobalInvocationID;
    if (id.x >= params.size || id.y >= params.size)
        return;
    
    vec4 data = imageLoad(PrecomputedData, ivec2(params.stage, id.y));
    uvec2 inputsIndices = uvec2(data.ba);
    
    if (params.ping_pong == 1u) {
        vec4 buffer_x = imageLoad(Buffer0, ivec2(id.x, inputsIndices.x));
        vec4 buffer_y = imageLoad(Buffer0, ivec2(id.x, inputsIndices.y));
        vec2 result = buffer_x.rg + ComplexMult(data.rg, buffer_y.rg);
        imageStore(Buffer1, ivec2(id.xy), vec4(result, 0.0, 0.0));
    } else {
        vec4 buffer_x = imageLoad(Buffer1, ivec2(id.x, inputsIndices.x));
        vec4 buffer_y = imageLoad(Buffer1, ivec2(id.x, inputsIndices.y));
        vec2 result = buffer_x.rg + ComplexMult(data.rg, buffer_y.rg);
        imageStore(Buffer0, ivec2(id.xy), vec4(result, 0.0, 0.0));
    }
}

void HorizontalStepInverseFFT() {
    uvec3 id = gl_GlobalInvocationID;
    if (id.x >= params.size || id.y >= params.size)
        return;
    
    vec4 data = imageLoad(PrecomputedData, ivec2(params.stage, id.x));
    uvec2 inputsIndices = uvec2(data.ba);
    
    if (params.ping_pong == 1u) {
        vec4 buffer_x = imageLoad(Buffer0, ivec2(inputsIndices.x, id.y));
        vec4 buffer_y = imageLoad(Buffer0, ivec2(inputsIndices.y, id.y));
        vec2 result = buffer_x.rg + ComplexMult(vec2(data.r, -data.g), buffer_y.rg);
        imageStore(Buffer1, ivec2(id.xy), vec4(result, 0.0, 0.0));
    } else {
        vec4 buffer_x = imageLoad(Buffer1, ivec2(inputsIndices.x, id.y));
        vec4 buffer_y = imageLoad(Buffer1, ivec2(inputsIndices.y, id.y));
        vec2 result = buffer_x.rg + ComplexMult(vec2(data.r, -data.g), buffer_y.rg);
        imageStore(Buffer0, ivec2(id.xy), vec4(result, 0.0, 0.0));
    }
}

void VerticalStepInverseFFT() {
    uvec3 id = gl_GlobalInvocationID;
    if (id.x >= params.size || id.y >= params.size)
        return;
    
    vec4 data = imageLoad(PrecomputedData, ivec2(params.stage, id.y));
    uvec2 inputsIndices = uvec2(data.ba);
    
    if (params.ping_pong == 1u) {
        vec4 buffer_x = imageLoad(Buffer0, ivec2(id.x, inputsIndices.x));
        vec4 buffer_y = imageLoad(Buffer0, ivec2(id.x, inputsIndices.y));
        vec2 result = buffer_x.rg + ComplexMult(vec2(data.r, -data.g), buffer_y.rg);
        imageStore(Buffer1, ivec2(id.xy), vec4(result, 0.0, 0.0));
    } else {
        vec4 buffer_x = imageLoad(Buffer1, ivec2(id.x, inputsIndices.x));
        vec4 buffer_y = imageLoad(Buffer1, ivec2(id.x, inputsIndices.y));
        vec2 result = buffer_x.rg + ComplexMult(vec2(data.r, -data.g), buffer_y.rg);
        imageStore(Buffer0, ivec2(id.xy), vec4(result, 0.0, 0.0));
    }
}

void Scale() {
    uvec3 id = gl_GlobalInvocationID;
    if (id.x >= params.size || id.y >= params.size)
        return;
    
    vec4 value = imageLoad(Buffer0, ivec2(id.xy));
    value = value / float(params.size * params.size);
    imageStore(Buffer0, ivec2(id.xy), value);
}

void Permute() {
    uvec3 id = gl_GlobalInvocationID;
    if (id.x >= params.size || id.y >= params.size)
        return;
    
    vec4 value = imageLoad(Buffer0, ivec2(id.xy));
    value = value * (1.0 - 2.0 * float((id.x + id.y) % 2u));
    imageStore(Buffer0, ivec2(id.xy), value);
}

void main() {
    if (params.mode == 0u) {
        HorizontalStepFFT();
    } else if (params.mode == 1u) {
        VerticalStepFFT();
    } else if (params.mode == 2u) {
        HorizontalStepInverseFFT();
    } else if (params.mode == 3u) {
        VerticalStepInverseFFT();
    } else if (params.mode == 4u) {
        Scale();
    } else if (params.mode == 5u) {
        Permute();
    }
}